valuetype ZoneRestriction oftype integer {
    constraints: [
        AreaLimit
    ];
}

constraint AreaLimit oftype RangeConstraint {
    lowerBound: 2001;
    lowerBoundInclusive: true;
    upperBound: 2001;
    upperBoundInclusive: true;
}

valuetype Coordinate oftype decimal {
    constraints: [
        LatitudeLongitudeRange
    ];
}

constraint LatitudeLongitudeRange oftype RangeConstraint {
    lowerBound: -90;
    lowerBoundInclusive: true;
    upperBound: 90;
    upperBoundInclusive: true;
}

pipeline PublicTransportDataIntegration {
    block GTFSDownloader oftype HttpExtractor {
        url: "https://gtfs.rhoenenergie-bus.de/GTFS.zip";
    }

    block ZipFileProcessor oftype ArchiveInterpreter {
        archiveType: "zip";
    }

    block StopsTxtSelector oftype FilePicker {
        path: "/stops.txt";
    }

    block TextDataInterpreter oftype TextFileInterpreter {}

    block CSVDataParser oftype CSVInterpreter {
        enclosing: '"';
        enclosingEscape: '"';
    }

    block StopsDataMapping oftype TableInterpreter {
        header: true;
        columns: [
            "stop_id" oftype integer,
            "stop_name" oftype text,
            "stop_lat" oftype Coordinate,
            "stop_lon" oftype Coordinate,
            "zone_id" oftype ZoneRestriction,
        ];
    }

    block SQLiteImport oftype SQLiteLoader {
        table: "stops";
        file: "./gtfs.sqlite";
    }

    GTFSDownloader
        -> ZipFileProcessor
        -> StopsTxtSelector
        -> TextDataInterpreter
        -> CSVDataParser
        -> StopsDataMapping
        -> SQLiteImport;
}
