valuetype Zone oftype integer {
    constraints: [Zone2001];
}

constraint Zone2001 oftype RangeConstraint {
    lowerBound: 2001;
    lowerBoundInclusive: true;
    upperBound: 2001;
    upperBoundInclusive: true;
}

valuetype Coordinate oftype decimal {
    constraints: [LatLonRange];
}

constraint LatLonRange oftype RangeConstraint {
    lowerBound: -90;
    lowerBoundInclusive: true;
    upperBound: 90;
    upperBoundInclusive: true;
}

pipeline exercise_5 {

    GTFSExtractor
        -> ZipFileInterpreter
        -> StopText
        -> StopTextInterpreter
        -> CSVDataParser
        -> StopMapping
        -> TableCreation;
        
    block GTFSExtractor oftype HttpExtractor {
        url: "https://gtfs.rhoenenergie-bus.de/GTFS.zip";
    }

    block ZipFileInterpreter oftype ArchiveInterpreter {
        archiveType: "zip";
    }

    block StopText oftype FilePicker {
        path: "/stops.txt";
    }

    block StopTextInterpreter oftype TextFileInterpreter {}

    block CSVDataParser oftype CSVInterpreter {
        enclosing: '"';
        enclosingEscape: '"';
    }

    block StopMapping oftype TableInterpreter {
        header: true;
        columns: [
            "stop_id" oftype integer,
            "stop_name" oftype text,
            "stop_lat" oftype Coordinate,
            "stop_lon" oftype Coordinate,
            "zone_id" oftype Zone,
        ];
    }

    block TableCreation oftype SQLiteLoader {
        table: "stops";
        file: "./gtfs.sqlite";
    }

}
